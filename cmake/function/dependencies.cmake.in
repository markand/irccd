#
# CMakeLists.txt -- CMake build system for irccd
#
# Copyright (c) 2016-2018 David Demelier <markand@malikania.fr>
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

include(GetPrerequisites)

set(TARGET $<TARGET_FILE:@TARGET@>)
set(BINDIR @CMAKE_INSTALL_BINDIR@)

get_prerequisites(
	${TARGET}
	DEPENDENCIES
	1 1
	""
	"@CMAKE_BINARY_DIR@/bin;@CMAKE_BINARY_DIR@/bin/$<CONFIG>"
)

foreach (dep ${DEPENDENCIES})
	#
	# Our libraries may already been installed, check the presence to
	# avoid recopying them.
	#
	if (NOT EXISTS ${CMAKE_INSTALL_PREFIX}/${BINDIR}/${dep})
		gp_resolve_item(
			${TARGET}
			${dep}
			""
			"@CMAKE_BINARY_DIR@/bin;@CMAKE_BINARY_DIR@/bin/$<CONFIG>"
			dll
		)

		if (EXISTS ${dll})
			message(STATUS "Installing dependency ${dll}")
			file(INSTALL ${dll} DESTINATION ${CMAKE_INSTALL_PREFIX}/${BINDIR})
		else ()
			message(WARNING "Dependency ${dep} not found")
		endif ()
	endif ()
endforeach ()
